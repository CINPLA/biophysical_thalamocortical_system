/*--------------------------------------------------------------
	TEMPLATE FILE FOR DEFINING CORTICAL PYRAMIDAL CELLS
	---------------------------------------------------

	SIMPLIFIED MODEL OF BURSTING PYRAMIDAL CELL

	- one compartment model and currents derived from:

  Destexhe, A. Contreras, D. and Steriade, M.  LTS cells in cerebral cortex
  and their role in generating spike-and-wave oscillations.   
  Neurocomputing 38: 555-563 (2001).

	Currents:

		- passive
		- HH Traub
		- IM
		- T-type calcium current


	Alain Destexhe, CNRS, 2000

--------------------------------------------------------------*/




begintemplate sPYr		// create a new template object
public soma

create soma[1]

proc init() { local v_potassium, v_sodium

  v_potassium = -100		// potassium reversal potential 
  v_sodium = 50			// sodium reversal potential 

  soma {
	Ra = 100		// geometry 
	nseg = 1
	diam = 96
	L = 96			// so that area is about 29000 um2
	cm = 1

	insert pas		// leak current 
	e_pas = -70
	g_pas = 1e-5		// idem TC cell

	// conversion with McC units: 
	// g(S/cm2) = g(nS)*1e-9/29000e-8
	//	    = g(nS) * 3.45e-6

	insert hh2cx		// Hodgin-Huxley INa and IK 
	ek = v_potassium
	ena = v_sodium
	vtraub_hh2cx = -55	// Resting Vm, BJ was -55
	gnabar_hh2cx = 0.05	// McCormick=15 muS, thal was 0.09
	gkbar_hh2cx = 0.005	// spike duration of pyr cells

	insert im		// M current 
	taumax_im = 1000
	gkbar_im = 3e-5		// specific to LTS pyr cell

	insert cad		// calcium decay
	depth_cad = 1		// McCormick= 0.1 um
	taur_cad = 5		// McCormick=1 ms !!!
	cainf_cad = 2.4e-4	// McCormick=0
	kt_cad = 0		// no pump

	insert it		// IT current 
	cai = 2.4e-4 
	cao = 2 
	eca = 120 
	gcabar_it = 0.001	// specific to LTS pyr cell


  }


}
endtemplate sPYr



